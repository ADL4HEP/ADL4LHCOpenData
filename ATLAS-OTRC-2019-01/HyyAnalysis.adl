info analysis
  title "Review of the 13 TeV ATLAS Open Data release"
  experiment ATLAS
  id "ATLOREACH-PUB-2020-001"
  publication " EPJ Web Conf., 245 (2020) 08026"
  sqrtS 13.0
  lumi 10000
  doi "10.1051/epjconf/202024508026"

SkipHistos = 1

object goodPhotons
    take PHO
    select photon_isTightID(PHO) == 1
    select Pt(PHO) > 25.0
    select photon_ptcone30(PHO)/photon_pt(PHO) < 0.065  # isolated photons
    select photon_etcone20(PHO)/photon_pt(PHO) < 0.065  # isolated photons
    select AbsEta(PHO) < 2.37
    select AbsEta(PHO) ][  1.37 1.52

define Photon1 = goodPhotons[0]
define Photon2 = goodPhotons[1]

define mYY  = sqrt( 2 * Pt(Photon1) * Pt(Photon2) * (cosh( dEta(Photon1,Photon2)) - cos(dPhi(Photon1,Photon2))))

define scaleFactor = scaleFactor_PHOTON*scaleFactor_PhotonTRIGGER*scaleFactor_PILEUP
define Lumi = 10064.0
define simWeight =  HSTEP(channelNumber > 310000) * mcWeight*Lumi*XSection*scaleFactor/SumWeights 
define dataWeight = HSTEP(channelNumber < 310000) * 1.0
define autoWeight = dataWeight + simWeight

region init
    select Size(goodPhotons) > 0 

    weight myweight autoWeight

region HyyAnalysis
    init
    select trigP == 1
    select Size(goodPhotons) == 2

    select Pt(Photon1)/mYY > 0.35
    select Pt(Photon2)/mYY > 0.25
    select mYY [] 105 160

    histo histmYYbin1 ,"Diphoton invariant mass; m_{#gamma#gamma} [GeV];Events / bin", 30, 105, 160, mYY
    
    select photon_convType(Photon1) == 0 && AbsEta(Photon1) < 0.75
    select photon_convType(Photon2) == 0 && AbsEta(Photon2) < 0.75
    
    histo histmYYcatbin1 ,"Diphoton invariant mass unconv. central; m_{#gamma#gamma} [GeV];Events / bin", 30, 105, 160, mYY