info analysis
  title "Review of the 13 TeV ATLAS Open Data release"
  experiment ATLAS
  id "ATLOREACH-PUB-2020-001"
  publication " EPJ Web Conf., 245 (2020) 08026"
  sqrtS 13.0
  lumi 10000
  doi "10.1051/epjconf/202024508026"

SkipHistos = 1



# OBJECT SELECTION

object goodEles
  take ELE
  select lep_isTightID(ELE) == 1 
  select lep_pt(ELE) > 35000.0
  select lep_ptcone30(ELE)/lep_pt(ELE) <   0.1   
  select lep_etcone20(ELE)/lep_pt(ELE) <   0.1      
  select abs(lep_eta(ELE)) <   2.47        
  select abs(lep_eta(ELE)) ][ 1.37 1.52   
  select Abs(lep_trackd0pvunbiased(ELE))/lep_tracksigd0pvunbiased(ELE) < 5.0
  select Abs(lep_z0(ELE))*sin(Theta(ELE))< 0.5


object goodMuos
  take MUO
  select lep_isTightID(MUO) == 1 
  select lep_pt(MUO) > 35000.0                  
  select lep_ptcone30(MUO)/lep_pt(MUO) <   0.1       
  select lep_etcone20(MUO)/lep_pt(MUO) <   0.1      
  select abs(lep_eta(MUO)) < 2.5 
  select Abs(lep_trackd0pvunbiased(MUO))/lep_tracksigd0pvunbiased(MUO) < 3.0
  select Abs(lep_z0(MUO)*sin(Theta(MUO))) < 0.5

object goodLepts : Union(goodEles, goodMuos)

object goodJets
  take JET
  select jet_pt(JET) > 30000
  select abs(jet_eta(JET)) < 2.5
  # JVT cleaning
  reject jet_pt(JET) < 60000 && ( abs(jet_eta(JET)) < 2.4 && jet_jvt(JET) < 0.59 )

object bJets
  take goodJets
  select jet_MV2c10(goodJets) > 0.8244273

object nonbJets
  take goodJets
  select jet_MV2c10(goodJets) < 0.8244273

### EVENT WEIGHTING VARIABLES
define scaleFactor = scaleFactor_ELE * scaleFactor_MUON * scaleFactor_LepTRIGGER * scaleFactor_BTAG * scaleFactor_PILEUP
define Lumi = 10064.0
define totWeight = mcWeight*Lumi*XSection*scaleFactor/SumWeights 
define totWeightAuto = HSTEP(channelNumber < 310000) * 1 + HSTEP(channelNumber > 310000) * totWeight

## OBJECT ALIASES
define leadLept  = goodLepts[0]
define bJet = bJets[0]
define nonbJet = nonbJets[0]

define lb = bJet leadLept
define mlb = m(lb)

define mtw = sqrt(2*Pt(leadLept)*MET*(1 - cos(dPhi(leadLept, METLV[0]))))
define HT = sum(Pt(goodEles))+ sum(Pt(goodMuos)) + sum(Pt(goodJets)) + MET 


# EVENT SELECTION

# Initialization of physic objects
region init
  select ALL
   select Size(goodEles) >= 0
   select Size(goodMuos) >= 0
   select Size(goodLepts) >= 0 
   sort Pt(goodLepts) descend
  select Size(goodJets) >= 0
  select Size(bJets) >= 0 
  select Size(nonbJets) >= 0

region singletop
  init
  weight evtweight totWeightAuto
  select trigE == 1 || trigM == 1
  select MET > 30.0
  select Size(goodLepts) == 1
  select Size(goodJets) == 2
  select Size(bJets) == 1
  select mtw > 60.0
  select mlb < 150
  select HT > 195
  select AbsEta(nonbJet) > 1.5
  select Abs( dEta(nonbJet, bJet) ) > 1.5

  histo hist_etmiss ,"Missing Transverse Momentum;E_{T}^{miss} [GeV];Events / bin", 20, 0, 200, MET
  histo hist_mtw, "Transverse Mass; M^{W}_{T} [GeV];Events / bin", 30, 0, 200, mtw
  histo hist_deta, "Delta eta; #Delta#eta(j,b);Events / bin", 20, 0, 5, dEta(nonbJet, bJet)
  
  histo hist_leadjetpt, "Untagged Jet Transverse Momentum;p_{T}^{jet} [GeV];Events / bin", 20, 0, 200, Pt(nonbJet)
  histo hist_leadjeteta, "Untagged Jet Pseudorapidity; #eta^{jet}; Events / bin", 20, -3, 3, Eta(nonbJet)
  histo hist_leadbjetpt, "Leading b-jet Transverse Momentum;p_{T}^{b-jet} [GeV];Events / bin", 20, 0, 200, Pt(bJet)
  histo hist_leadbjeteta, "Leading b-jet Pseudorapidity; #eta^{b-jet}; Events / bin", 20, -3, 3, Eta(bJet)