info analysis
  title "Review of the 13 TeV ATLAS Open Data release"
  experiment ATLAS
  id "ATLOREACH-PUB-2020-001"
  publication " EPJ Web Conf., 245 (2020) 08026"
  sqrtS 13.0
  lumi 10000
  doi "10.1051/epjconf/202024508026"

SkipHistos = 1

# OBJECT SELECTION

object goodEles
  take ELE
  select lep_isTightID(ELE) == 1 
  select Pt(ELE) > 35.0
  select AbsEta(ELE) <   2.47        
  select AbsEta(ELE) ][ 1.37 1.52   
  select lep_ptcone30(ELE)/lep_pt(ELE) <   0.1      
  select lep_etcone20(ELE)/lep_pt(ELE) <   0.1      
  select Abs(lep_trackd0pvunbiased(ELE))/lep_tracksigd0pvunbiased(ELE) < 5.0
  select Abs(lep_z0(ELE))*sin(theta(ELE))< 0.5

object goodMuos
  take MUO
  select lep_isTightID(MUO) == 1 
  select Pt(MUO) > 35.0
  select AbsEta(MUO) < 2.5 
  select lep_ptcone30(MUO)/lep_pt(MUO) <   0.1       
  select lep_etcone20(MUO)/lep_pt(MUO) <   0.1      
  select Abs(lep_trackd0pvunbiased(MUO))/lep_tracksigd0pvunbiased(MUO) < 3.0
  select Abs(lep_z0(MUO)*sin(theta(MUO))) < 0.5

object goodLepts : Union(goodEles, goodMuos)

## WEIGHTING VARIABLES
define sfc = scaleFactor_ELE*scaleFactor_MUON*scaleFactor_LepTRIGGER*scaleFactor_PILEUP
define Lumi = 10064.0
define mcMcWeight = HSTEP(channelNumber > 310000) * mcWeight*Lumi*XSection*sfc/SumWeights 
define dataWeight = HSTEP(channelNumber < 310000) * 1.0
define totalWeight = dataWeight + mcMcWeight

define leadLept = goodLepts[0]
define mtw = sqrt(2*Pt(leadLept)*MET*(1 - cos(dPhi(leadLept, METLV[0]))))
define ptisovar = lep_ptcone30(leadLept)/lep_pt(leadLept)

## EVENT SELECTION
region init
  select ALL
  select Size(goodEles) >= 0
  select Size(goodMuos) >= 0
  select Size(goodLepts) >= 0
  sort Pt(goodLepts) descend
 
region Wlnu
  init
  weight evtWeight totalWeight
  select trigE == 1 || trigM == 1
  select MET > 30.0  
  select Size(goodLepts) == 1
  select mtw > 60.0
  
  ## Global variables
  histo hist_etmiss   , "Missing Transverse Momentum;E_{T}^{miss} [GeV];Events / bin", 30, 0, 200, MET
  histo hist_mtw      , "Transverse Mass; M^{W}_{T} [GeV];Events / bin", 40, 39, 180, mtw
  
  #  Leading jet variables
  histo hist_n_jets      , "Number of Jets;N_{jets};Events", 6, -0.5, 5.5, Size(JET)
  histo hist_leadjet_pt  , "Leading Jet Transverse Momentum;p_{T}^{jet} [GeV];Events / bin", 20, 0, 120, Pt(JET)
  histo hist_leadjet_eta , "Leading Jet Pseudorapidity; #eta^{jet}; Events / bin", 30, -3, 3, Eta(JET)

  # Leading lepton variables
  histo hist_leadleptpt  , "Lepton Transverse Momentum;p_{T}^{lep} [GeV];Events / bin", 20, 0, 200, Pt(leadLept)
  histo hist_leadlepteta , "Lepton Pseudorapidity; #eta^{lep}; Events / bin", 30, -3, 3, Eta(leadLept)
  histo hist_leadleptE   , "Lepton Energy; E^{lep} [GeV]; Events / bin", 30, 0, 300, E(leadLept)
  histo hist_leadleptphi , "Lepton Azimuthal Angle ; #phi^{lep}; Events / bin", 32, -3.2, 3.2, Phi(leadLept)
  histo hist_leadleptch  , "Lepton Charge; Q^{lep}; Events / bin", 7, -1.75, 1.75, q(leadLept)
  histo hist_leadleptID  , "Lepton Absolute PDG ID; |PDG ID|^{lep}; Events / bin",  15, 5.5, 20.5, Abs(pdgID(leadLept))

region Wenu
  Wlnu
  select Abs(pdgID(leadLept)) == 11
  histo hist_mtw_enu  , "Transverse Mass; M^{W #rightarrow e#nu}_{T} [GeV];Events / bin", 40, 39, 180, mtw

region Wmunu
  Wlnu
  select Abs(pdgID(leadLept)) == 13
  histo hist_mtw_munu , "Transverse Mass; M^{W #rightarrow #mu#nu}_{T} [GeV];Events / bin", 40, 39, 180, mtw
